// SPDX-License-Identifier: MIT

// ----------------------------------------------------------------------------
// Developed by:
// Sueun Cho and DoHyun Lim
// If you find our code helpful or cool, consider buying us a beer! üçª Cheers!
// ----------------------------------------------------------------------------

pragma solidity ^0.8.4;

import "https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/token/ERC721/ERC721.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";
import "https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/access/Ownable.sol";

contract MarketPlace is IERC721Receiver, Ownable, ReentrancyGuard {
    uint256 public nftListedCount = 0;
    address public allowedNftAddress;
    address payable thisAddress;
    
    mapping(uint256 => ActiveList) public fixedAuctions;
    mapping(uint256 => EnglandAuction) public englandAuctions;
    mapping(uint256 => DutchAuction) public dutchAuctions;

    ERC721 nft;

    struct ActiveList {
        uint256 NFTId;
        address payable NFTSeller;
        address payable thisAddress;
        uint256 NFTPrice;
        bool isSold;
    }

    struct EnglandAuction {
        uint256 NFTId;
        address payable seller;
        uint256 startingPrice;
        uint256 endTime;
        address payable highestBidder;
        uint256 highestBid;
        bool ended;
        bool cancelled;
        uint256 dueDate;
    }

    struct DutchAuction {
        uint256 NFTId;
        address payable seller;
        uint256 startingPrice;
        uint256 finalLowPrice;
        uint256 declineRate;
        uint256 startTime;
        bool ended;
    }

    event NFTListCreated (
        uint256 indexed NFTId,
        address NFTSeller,
        address thisAddress,
        uint256 NFTPrice,
        bool isSold
    );

    event AuctionCreated(uint256 indexed NFTId, uint256 startingPrice, uint256 endTime);
    event NewBid(uint256 indexed NFTId, address bidder, uint256 amount);
    event AuctionEnded(uint256 indexed NFTId, address winner, uint256 amount);

    constructor(ERC721 _nft) Ownable(msg.sender) {
        thisAddress = payable(msg.sender);
        nft = _nft;
        allowedNftAddress = address(_nft);
    }

    function checkTime() public view returns(uint256){
        return block.timestamp;
    }

    function isAllowedNFT(address _nftAddress) internal view returns(bool) {
        return _nftAddress == allowedNftAddress;
    }

    //AUDIT END - SUEUN CHO
    // Ïñ¥Îñ§ NFTÎ•º ÏÇ¥Í±¥ÏßÄ = NFTId
    function buyListedNft(uint256 NFTId) public payable nonReentrant {
        ActiveList storage auction = fixedAuctions[NFTId];
        uint256 NFTPrice = auction.NFTPrice;
    
        require(msg.value == NFTPrice, "msg.value and NFTPrice are not same");
        require(!auction.isSold, "NFT already sold");
    
        // Î∞îÎ°ú ÏÇ≠Ï†úÌïòÍ∏∞ Ï†ÑÏóê ÌåêÎß§Ïûê Ï£ºÏÜåÎ•º ÏûÑÏãú Î≥ÄÏàòÏóê Ï†ÄÏû•Ìï©ÎãàÎã§.
        address payable sellerAddress = auction.NFTSeller;
    
        // ÌåêÎß§Îêú ÏÉÅÌÉúÎ°ú ÌëúÏãú
        auction.isSold = true;
        
        // NFTÎ•º Íµ¨Îß§ÏûêÏóêÍ≤å Ï†ÑÏÜ°
        nft.safeTransferFrom(address(this), msg.sender, NFTId); 
    
        // ÌåêÎß§ÏûêÏóêÍ≤å ether Ï†ÑÏÜ°
        sellerAddress.transfer(msg.value);
    
        // Ïù¥Ï†ú Ìï¥Îãπ Î™©Î°ùÏùÑ ÏÇ≠Ï†úÌï©ÎãàÎã§.
        delete fixedAuctions[NFTId];
    }


    //AUDIT END - SUEUN CHO
    //fixedAuctionsÏóê ÌïúÏ†ïÎêú ÏΩîÎìú
    function cancelNFTSale(uint256 NFTId) public nonReentrant {
        //nft ÌåêÎß§ÏûêÏù∏ÏßÄ ÌôïÏù∏
        require(fixedAuctions[NFTId].NFTSeller == msg.sender, "NFT is not yours");
        nft.safeTransferFrom(address(this), msg.sender, NFTId);
        nftListedCount--;
        delete fixedAuctions[NFTId];
    }

    //AUDIT END - SUEUN CHO
    //fixedAuctionsÏóê ÌïúÏ†ïÎêú ÏΩîÎìú
    function NFTlistSale(uint256 NFTId, uint256 NFTPrice) public payable nonReentrant {
        //Î®ºÏ†Ä NFTÍ∞Ä Î≥∏Ïù∏ÍªÄÏßÄ ÌôïÏù∏
        require(nft.ownerOf(NFTId) == msg.sender, "NFT is not yours"); 
        ////IDÍ∞Ä 0Î≤àÏù∏ NFTÎäî Ïñ¥ÎñªÍ≤å Ï≤òÎ¶¨Ìï†Í≤ÉÏù∏Í∞Ä?
        require(fixedAuctions[NFTId].NFTId == 0, "NFT already listed"); 
        //ÎÇ¥Í∞Ä Ï†ïÌïú NFT Ïª®Ìä∏ÎûôÌä∏Îßå ÏÇ¨Ïö©Í∞ÄÎä•
        require(isAllowedNFT(address(nft)), "Only allowed NFT can be listed");
        require(NFTPrice > 0, "NFT Price over than 0!"); 

        fixedAuctions[NFTId] =  ActiveList(NFTId, payable(msg.sender), thisAddress, NFTPrice, false);
        nft.safeTransferFrom(msg.sender, address(this), NFTId);
        nftListedCount++;
        emit NFTListCreated(NFTId, msg.sender, address(this), NFTPrice, false);
    }

//    //Ï¢ÄÎçî Ï¢ãÏùÄ Î∞©Ìñ•ÏúºÎ°ú ÏàòÏ†ïÌïÑÏöî
//    function showNFTListings() public view returns (ActiveList[] memory) {
//        ActiveList[] memory listItems = new ActiveList[](nftListedCount);
//        uint256 counter = 0;
//        for (uint256 i = 1; i <= nftListedCount; i++) {
//            if (fixedAuctions[i].thisAddress == address(this)) {
//                listItems[counter] = fixedAuctions[i];
//                counter++;
//            }
//        }
//        return listItems;
//    }

    //AUDIT END - SUEUN CHO
    function createAuction(uint256 NFTId, uint256 startingPrice, uint256 duration) public payable nonReentrant {
        require(nft.ownerOf(NFTId) == msg.sender, "NFT is not yours");
        require(isAllowedNFT(address(nft)), "Only allowed NFT can be listed");
        require(startingPrice > 0 && duration > 60 , "NFT Price over than 0! and over 60 sec(approx) of duration");
        englandAuctions[NFTId] = EnglandAuction({
            NFTId: NFTId,
            seller: payable(msg.sender),
            startingPrice: startingPrice,
            endTime: block.timestamp + duration,
            highestBidder: payable(address(0)),
            highestBid: 0,
            ended: false,
            cancelled: false,
            dueDate : duration
        });

        // Contract AddressÎ°ú NFT Ïù¥Îèô. Î≥∏Ïù∏(msg.sender)Ïù¥ Ïù¥ Ï£ºÏÜåÎ°ú NÎ≤àÏß∏ nft Ïù¥Îèô
        nft.transferFrom(msg.sender, address(this), NFTId);

        // Event log
        emit AuctionCreated(NFTId, startingPrice, block.timestamp + duration);
    }

    //AUDIT END - SUEUN CHO
    function bid(uint256 NFTId) public payable nonReentrant {
        EnglandAuction storage auction = englandAuctions[NFTId];
        require(block.timestamp < auction.endTime, "England auction already done");
        require(msg.value > auction.highestBid, "There is a higher bid");
        require(isAllowedNFT(address(nft)), "Only allowed NFT can be listed");

        if (auction.highestBid != 0) { // auction.highestBid != 0 : Í≤ΩÎß§Í∞Ä ÏãúÏûë Îê®
        //Í∑∏ÏàúÍ∞Ñ ÏµúÍ≥† ÏûÖÏ∞∞ÏûêÏòÄÎçò ÏÇ¨ÎûåÏóêÍ≤å ÏûÖÏ∞∞ Í∏àÏï°ÏùÑ ÎèåÎ†§Ï§å
            auction.highestBidder.transfer(auction.highestBid);
        }

        // payableÎ°ú Ïù∏ÌïòÏó¨ Ïù¥Î†áÍ≤å msg.senderÍ∞Ä Î∞õÏùÑ Ïàò ÏûàÏùå
        auction.highestBidder = payable(msg.sender);
        auction.highestBid = msg.value;
        
        emit NewBid(NFTId, msg.sender, msg.value);
    }

    //AUDIT END - SUEUN CHO
    function endAuction(uint256 NFTId) public nonReentrant {
        EnglandAuction storage auction = englandAuctions[NFTId];
        require(block.timestamp > auction.endTime, "England auction not over yet");
        require(!auction.ended, "England auction has already been done");

        auction.ended = true;

        if (auction.highestBidder != address(0)) { // Ï£ºÏÜåÍ∞Ä ÏûàÎã§Î©¥,
            // If there was at least one bid, transfer funds to the seller
            // auction sellerÍ∞Ä ÎÜíÏùÄ Í∏àÏï° Ï†ÑÏÜ°
            auction.seller.transfer(auction.highestBid);
            // ContractÏóêÏÑú highestBidderÏóêÍ≤å NFT Ï†ÑÏÜ°
            nft.transferFrom(address(this), auction.highestBidder, NFTId);
            emit AuctionEnded(NFTId, auction.highestBidder, auction.highestBid);
        } else {
            // If there were no bids, return NFT to the seller
            nft.transferFrom(address(this), auction.seller, NFTId);
            emit AuctionEnded(NFTId, auction.seller, 0);
        }
    }

    //AUDIT END - SUEUN CHO
    function createDutchAuction(uint256 NFTId, uint256 startingPrice, uint256 finalLowPrice, uint256 declineRate) public payable nonReentrant {
        require(nft.ownerOf(NFTId) == msg.sender, "NFT is not yours!");
        require(isAllowedNFT(address(nft)), "Only allowed NFT can be listed");

        dutchAuctions[NFTId] = DutchAuction({
            NFTId: NFTId,
            seller: payable(msg.sender),
            startingPrice: startingPrice,
            finalLowPrice: finalLowPrice,
            declineRate: declineRate,
            startTime: block.timestamp,
            ended: false
        });

        nft.transferFrom(msg.sender, address(this), NFTId);
    }

    //AUDIT END - SUEUN CHO
    function getCurrentPrice(uint256 NFTId) public view returns (uint256) {
        DutchAuction storage auction = dutchAuctions[NFTId];
        uint256 elapsedTime = block.timestamp - auction.startTime;
        uint256 priceDecline = elapsedTime * auction.declineRate;
        uint256 currentPrice = auction.startingPrice - priceDecline;

        return currentPrice > auction.finalLowPrice ? currentPrice : auction.finalLowPrice;
    }

    //AUDIT END - SUEUN CHO
    function buyDutchAuction(uint256 NFTId) public payable nonReentrant {
        DutchAuction storage auction = dutchAuctions[NFTId];
        uint256 currentPrice = getCurrentPrice(NFTId);
        require(msg.value >= currentPrice, "The value is below the current price");
        require(!auction.ended, "Dutch Auction already done");

        auction.ended = true;

        auction.seller.transfer(currentPrice);

        nft.transferFrom(address(this), msg.sender, NFTId);
    }

    //AUDIT END - SUEUN CHO
    function cancelDutchAuction(uint256 NFTId) public nonReentrant {
        DutchAuction storage auction = dutchAuctions[NFTId];
        require(msg.sender == auction.seller, "You are not the seller!");
        require(!auction.ended, "Dutch Auction already done");
        
        uint256 currentPrice = getCurrentPrice(NFTId);
        require(currentPrice <= auction.finalLowPrice, "The auction cannot be cancelled above reserve price!");
    
        auction.ended = true;
        
        nft.transferFrom(address(this), msg.sender, NFTId);
    }

    //AUDIT END - SUEUN CHO
    function onERC721Received(
        address,
        address from,
        uint256,
        bytes calldata
    ) external pure override returns (bytes4) {
        require(from != address(0x0), "Cannot send nfts to Vault directly");

        return IERC721Receiver.onERC721Received.selector;
    }
}